% Esercizio 2.
%
% 5 scrittori hanno pubblicato i loro nuovi romanzi gialli. Un appassionato li ha 
% letti, e vuole rovinarvi la sorpresa facendovi indovinare i finali di ciascuno. 
% I 5 assassini (1 per romanzo) sono: maggiordomo, artista, dentista, idraulico, 
% avvocato. I 5 luoghi (1 per romanzo) sono: Roma, Milano, Falerna, Napoli, 
% Lamezia Terme. Le 5 vittime (1 per romanzo) sono: cugino, amico, madre, partner, 
% sorella. I 5 moventi (1 per romanzo) sono: ricatto, eredità, amore, potere, 
% vendetta. Dovete quindi ricostruire i finali (cioè quale assassino ha ucciso 
% quale vittima, dove lo ha fatto e quale era il movente). Gli indizi che vi 
% fornisce sono i seguenti:
%
% 1. Né il maggiordomo, né l’idraulico hanno ucciso a Roma. 
% 
% 2. L’omicidio avvenuto a Roma aveva come movente l’eredità. 
% 
% 3. L’omicidio avvenuto per vendetta non è stato perpetrato né a Falerna né a 
% Milano. L’artista non ha ucciso il partner (il quale a sua volta non è stato 
% ucciso né per vendetta né per potere). 
% 
% 4. Il dentista ha ucciso un cugino a Lamezia Terme, ma non per vendetta, né per 
% amore. 
% 
% 5. La sorella non è stata uccisa a Roma, né a Falerna; e la vittima di Falerna 
% non è stata uccisa per amore. Il maggiordomo non ha ucciso la partner. 
% 
% 6. Nel romanzo in cui l’assassino è l’avvocato il  movente è il potere, ma la 
% vittima non è un amico.

assassino(maggiordomo).
assassino(artista).
assassino(dentista).
assassino(idraulico).
assassino(avvocato).

luogo(roma).
luogo(milano).
luogo(falerna).
luogo(napoli).
luogo(lamezia).

vittima(cugino).
vittima(amico).
vittima(madre).
vittima(partner).
vittima(sorella).

movente(ricatto).
movente(eredita).
movente(amore).
movente(potere).
movente(vendetta).

% Guess
assassinoLuogo(X,roma) | assassinoLuogo(X,milano) | assassinoLuogo(X,falerna) | assassinoLuogo(X,napoli) | assassinoLuogo(X,lamezia) :- assassino(X).
assassinoVittima(X,cugino) | assassinoVittima(X,amico) | assassinoVittima(X,madre) | assassinoVittima(X,partner) | assassinoVittima(X,sorella) :- assassino(X).
assassinoMovente(X,ricatto) | assassinoMovente(X,eredita) | assassinoMovente(X,amore) | assassinoMovente(X,potere) | assassinoMovente(X,vendetta) :- assassino(X).

% Aux rules
%% Mostra i finali con il seguente format: Assassino - Luogo - Vittima - Movente
finale(A,L,V,M) :- assassinoLuogo(A,L), assassinoVittima(A,V), assassinoMovente(A,M).

% Check
%% Non è possibile che ci siano due assassini che uccidono nello stesso luogo
:- assassinoLuogo(A1,L), assassinoLuogo(A2,L), A1!=A2.

%% Non è possibile che ci siano due assassini che uccidono la stessa vittima
:- assassinoVittima(A1,V), assassinoVittima(A2,V), A1!=A2.

%% Non è possibile che ci siano due assassini che uccidono per lo stesso movente
:- assassinoMovente(A1,M), assassinoMovente(A2,M), A1!=A2.

%% Indizi
:- assassinoLuogo(maggiordomo,roma).                            % 1a
:- assassinoLuogo(idraulico,roma).                              % 1b

:- assassinoLuogo(X,roma), not assassinoMovente(X,eredita).     % 2

:- assassinoMovente(X,vendetta), assassinoLuogo(X,falerna).     % 3a
:- assassinoMovente(X,vendetta), assassinoLuogo(X,milano).      % 3b
:- assassinoVittima(artista,partner).                           % 3c
:- assassinoVittima(X,partner), assassinoMovente(X,vendetta).   % 3d
:- assassinoVittima(X,partner), assassinoMovente(X,potere).     % 3e

:- assassinoMovente(dentista,vendetta).                         % 4a
:- assassinoMovente(dentista,amore).                            % 4b
assassinoLuogo(dentista,lamezia).                               % Per il vincolo 4
assassinoVittima(dentista,cugino).                              % Per il vincolo 4

:- assassinoVittima(X,sorella), assassinoLuogo(X,roma).         % 5a
:- assassinoVittima(X,sorella), assassinoLuogo(X,falerna).      % 5b
:- assassinoLuogo(X,falerna), assassinoMovente(X,amore).        % 5c
:- assassinoVittima(maggiordomo,partner).                       % 5d

:- assassinoVittima(avvocato,amico).                            % 6
assassinoMovente(avvocato,potere).                              % Per il vincolo 6