%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% ESERCIZIO 3: bambini e pastelli
%
% Quattro bambini, Caterina, Federico, Cristina e Marcello, hanno fatto dei disegnini
% all’asilo. Ciascuno di loro ha utilizzato uno strumento differente. I bambini hanno
% mostrato con orgoglio i loro lavori al resto della classe in un certo ordine. Scrivere
% un programma DLV che determini l’ordine in cui ciascun bambino ha presentato il proprio
% disegno e quale strumento ha usato per prepararlo, dati i seguenti indizi:
% - Marcello, che è stato l’ultimo a mostrare il proprio disegno, non ha usato l’evidenziatore.
% - Il bambino che ha usato la matita ha mostrato il proprio disegno immediatamente dopo quello
%   che ha utilizzato il pennello.
% - Caterina non è stata la seconda.
% - Cristina ha mostrato il proprio disegno due turni dopo quello del bambino che ha usato
%   il pastello.

bambino(caterina).
bambino(federico).
bambino(cristina).
bambino(marcello).

% Guess
posizione(B,1) | posizione(B,2) | posizione(B,3) | posizione(B,4) :- bambino(B).
usa(B,evidenziatore) | usa(B,matita) | usa(B,pennello) | usa(B,pastello) :- bambino(B).

% Per stampa finale
presentazione(P,B,S) :- posizione(B,P), usa(B,S).

% Check
%% Non è possibile che due bambini presentino nello stesso momento
:- posizione(B1,X), posizione(B2,X), B1<>B2.

%% Non è possibile che due bambini utilizzino lo stesso strumento
:- usa(B1,S), usa(B2,S), B1<>B2.

%% Marcello, che è stato l’ultimo a mostrare il proprio disegno, non ha usato l’evidenziatore.
posizione(marcello,4).
:- usa(marcello,evidenziatore).

%% Il bambino che ha usato la matita ha mostrato il proprio disegno immediatamente dopo quello
%% che ha utilizzato il pennello.
:- usa(B1,matita), posizione(B1,P1), usa(B2,pennello), posizione(B2,P2), P1!=P2+1.

%% Caterina non è stata la seconda.
:- posizione(caterina,2).

%% Cristina ha mostrato il proprio disegno due turni dopo quello del bambino che ha usato
%% il pastello.
:- posizione(cristina,P1), usa(B,pastello), posizione(B,P2), P1!=P2+2.