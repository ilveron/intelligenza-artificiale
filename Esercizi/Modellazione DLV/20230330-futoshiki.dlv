% Esercizio 1. 
%
% Si prenda in considerazione il gioco giapponese chiamato “FUTOSHIKI”. 
% In questo gioco matematico si deve riempire una griglia quadrata contenente NxN celle, 
% di modo che ogni riga o colonna contenga tutti e soli i numeri da 1 a N. Sono inoltre 
% presenti vincoli tra alcune celle adiacenti, espressi con un segno di maggiore 
% (o minore, a seconda del punto di vista), che devono ovviamente essere rispettati: 
% se tra due celle adiacenti è presente la diseguaglianza, i numeri collocati dovranno rispettarne il verso. 
% Nella griglia di input possono essere già presenti alcuni numeri che aiutano la risoluzione. 
% Si scriva un programma logico ASP che consenta ad un sistema (come ad esempio DLV) di risolvere il rompicapo: 
% si modelli adeguatamente il problema definendo in modo appropriato la struttura delle istanze in input, 
% e si sfrutti poi il paradigma “guess&check” per ottenere la soluzione.

riga(1..5).
colonna(1..5).

valore(1..5).

cella(X,Y) :-riga(X), colonna(Y).

maggiore(1,4,1,5).
maggiore(1,3,2,3).
maggiore(2,4,2,3).
maggiore(3,2,2,2).
maggiore(2,3,3,3).
maggiore(3,4,2,4).
maggiore(4,1,4,2).
maggiore(4,5,4,4).
maggiore(5,2,5,3).
maggiore(5,3,4,3).
maggiore(4,4,5,4).

contiene(2,5,2).

% Guess
contiene(R,C,V) | nonContiene(R,C,V) :- cella(R,C), valore(V).

% Aux rules
rigaValore(R,V) :- contiene(R,_,V).
colonnaValore(C,V) :- contiene(_,C,V).

% Check
%% Non è possibile che ci siano due colonne sulla stessa riga che hanno lo stesso valore
:- contiene(R,C1,V), contiene(R,C2,V), C1<>C2.

%% Non è possibile che ci siano due righe sulla stessa colonna che hanno lo stesso valore
:- contiene(R1,C,V), contiene(R2,C,V), R1<>R2.

%% Non è possibile che il vincolo sul maggiore non sia rispettato
:- maggiore(R1,C1,R2,C2), contiene(R1,C1,V1), contiene(R2,C2,V2), V2>V1.

%% Non è possibile che ci siano due valori nella stessa cella
:- contiene(R1,C1,V1), contiene(R1,C1,V2), V1<>V2. 

%% Non è possibile che ci sia un valore mancante su una riga        (Questo vincolo e quello di sotto ci permettono di dire che non è possibile che ci sia una cella che non contiene valori)
:- valore(V), riga(R), not rigaValore(R,V).

%% Non è possibile che ci sia un valore mancante su una colonna     (Questo vincolo e quello di sopra ci permettono di dire che non è possibile che ci sia una cella che non contiene valori)
:- valore(V), colonna(C), not colonnaValore(C,V).

%%%% Oppure... (Soluzione prof., con questa non servono i due vincoli di sopra e le aux rules)
% piena(X,Y) :- contiene(X,Y,_).
% :- cella(X,Y), not piena(X,Y).