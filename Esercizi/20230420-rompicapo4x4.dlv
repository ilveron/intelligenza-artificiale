% Si consideri il seguente rompicapo. Obiettivo del gioco e' completare una griglia di dimensione 4x4 con numeri che vanno da 1 a 9, rispettando le seguenti semplici regole:  
% 1)	Ogni riga deve contenere numeri tutti diversi tra loro;
% 2)	Ogni colonna deve contenere numeri tutti diversi tra loro;
% 3)	La somma dei valori in una riga deve essere uguale al valore assegnato per quella riga;
% 4)	La somma dei valori in una colonna deve essere uguale al valore assegnato per quella colonna;
% 5)	La somma dei valori sulla diagonale principale deve essere uguale ad un valore assegnato;

% Sono inoltre fornite le seguenti indicazioni:

% 6)	Nella prima riga è presente un 5;
% 7)	La terza riga e la terza colonna contengono valori disposti in ordine crescente;
% 8)	Il numero 3 compare esattamente una volta in tutta la griglia.

% Si risolva tale rompicapo tramite un opportuno programma dlv.
% Esempio:  Si consideri la seguente griglia 4x 4, con indicati i valori delle somme degli elementi sulle righe (12,14, 30 e 17), sulle colonne (17,15,23,18) e sulla diagonale principale (il 15 nell’angolo in basso).

%				    12
%				    14
%				    30
%				    17
%   17	15	23	18	15


% la soluzione del rompicapo è:
%    4	 1	 2	 5	12
%    5	 2	 4	 3	14
%    6	 7	 8	 9	30
%    2	 5	 9	 1	17
%   17	15	23	18	15

val(1..9).
row(1..4).
col(1..4).
cell(R,C) :- row(R), col(C).
rowSum(1,12).
rowSum(2,14).
rowSum(3,30).
rowSum(4,17).
colSum(1,17).
colSum(2,15).
colSum(3,23).
colSum(4,18).
diagSum(15).

% Guess
in(R,C,V) | out(R,C,V) :- cell(R,C), val(V). 

% Check 
%% Non è possibile che nella stessa cella ci siano 2 valori
:- in(R,C,V1), in(R,C,V2), V1<>V2.

%% Non è possibile che ci siano celle senza valore
filledCell(R,C) :- in(R,C,_).
:- cell(R,C), not filledCell(R,C).

%% Non è possibile che nella stessa riga ci sia due volte lo stesso valore
:- in(R,C1,V), in(R,C2,V), C1<>C2.

%% Non è possibile che nella stessa colonna ci sia due volte lo stesso valore
:- in(R1,C,V), in(R2,C,V), R1<>R2.

%% Non è possibile che la somma dei valori di una riga non corrisponda a quella data in input
:- #sum{V,C : in(R,C,V)}<>S, rowSum(R,S).

%% Non è possibile che la somma dei valori di una colonna non corrisponda a quella data in input
:- #sum{V,R : in(R,C,V)}<>S, colSum(C,S).

%% Non è possibile che la somma dei valori della diagonale non corrisponda a quella data in input
:- #sum{V,X : in(X,X,V)}<>S, diagSum(S).

%% Non è possibile che nella prima riga non sia presente un 5
rowVal(R,V) :- in(R,_,V).
:- not rowVal(1,5).

%% Non è possibile che la terza riga non abbia valori disposti in modo crescente
:- in(3,C1,V1), in(3,C2,V2), C1<C2, V1>V2. 

%% Non è possibile che la terza colonna non abbia valori disposti in modo crescente
:- in(R1,3,V1), in(R2,3,V2), R1<R2, V1>V2. 

%% Non è possibile che il 3 non compaia esattamente una volta in tutta la griglia
:- #count{R,C : in(R,C,3)}<>1.